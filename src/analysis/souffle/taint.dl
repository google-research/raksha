//-----------------------------------------------------------------------------
// Copyright 2021 Google LLC
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     https://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
//-----------------------------------------------------------------------------

#ifndef SRC_ANALYSIS_SOUFFLE_TAINT_DL_
#define SRC_ANALYSIS_SOUFFLE_TAINT_DL_

#include "authorization_logic.dl"
#include "dataflow_graph.dl"
#include "tags.dl"

//-----------------------------------------------------------------------------
// Relation Declarations
//-----------------------------------------------------------------------------

// Indicates that an access path definitely has a tag. Differs from mayHaveTag
// below in that mayHaveTag indicates only that it is possible that an
// accessPath has a tag.
.decl hasTag(accessPath: AccessPath, owner: Principal, tag: Tag)

// Declares that `accessPath` may have been tainted with `tag`.
// If false, `accessPath` *definitely* does not have `tag` taint at runtime.
// If true, `accessPath` may have the `tag` taint at runtime.
// NOTE: In practice there may be value in explicitly having a layer of
// indirection in which accessPaths have IFCLabels and just IFCLabels have Tags.
// In this formalization, accessPaths also act as IFCLabels (in the sense that
// they have Tags).
.decl mayHaveTag(accessPath: AccessPath, owner: Principal, tag: Tag)

// The tag for some accessPath is actually removed (by taking into consideration
// the privileges of a party trying to remove it, and as a result of that party
// deciding to remove it). Because here IFC labels are sets of tags (ordered
// by the subset or equals relation), removing a tag from the set corresponds to
// a downgrade by making set of tags on the access path "lower" in the order.
.decl removeTag(path: AccessPath, owner: Principal, tag: Tag)


// Manifests can produce base facts of this form, where usually the principal
// is a particle (and this fact is entered by a code reviewer with no knowledge
// of the taint analysis).
.decl claimNotEdge(principal: Principal, src: AccessPath, dst: AccessPath)

// Indicates that an AccessPath is given an integrity tag directly. This rule
// should only be populated with base facts with no derivation to prevent us
// from getting tangled up in stratification issues. Propagation will happen
// via the mayHaveInvIntegTag rule.
.decl directHasIntegTag(accessPath: AccessPath, integTag: IntegTag)

// Integrity tag universe.
.decl isIntegTag(integTag: IntegTag)

// Removes an integrity tag from some path. Effectively adds an InvIntegTag.
.decl removeIntegTag(accessPath: AccessPath, integTag: IntegTag)

// Indicates that a particular tag may have an inverse integrity tag, which
// indicates that it should not have an integrity tag.
.decl mayHaveInvIntegTag(accessPath: AccessPath, invIntegTag: InvIntegTag)

// Used to test whether a particular path has a particular integrity tag.
// Should be used as close to top-level checks as possible, and not for
// propagation, to prevent stratification issues.
.decl mustHaveIntegTag(accessPath: AccessPath, integTag: IntegTag)

// Maps between integrity tags and inverse integrity tags.
.decl integTagToInv(integTag: IntegTag, invIntegTag: InvIntegTag)

//-----------------------------------------------------------------------------
// Rules
//-----------------------------------------------------------------------------

ownsAccessPath(principal, path) :- says_ownsAccessPath(principal, principal, path).
ownsAccessPath(principal, tgt) :-
  resolvedEdge(principal, src, tgt), ownsAccessPath(principal, src).

// This rule says that a tag may be removed only if the owner of the tag says so.
removeTag(path, owner, tag) :-
   says_removeTag(owner, path, owner, tag).

hasTag(path, owner, tag) :- says_hasTag(owner, path, owner, tag).

mayHaveTag(tgt, owner, tag) :- hasTag(tgt, owner, tag).

mayHaveTag(tgt, owner, tag) :-
    resolvedEdge(owner, src, tgt), mayHaveTag(src, owner, tag), !removeTag(tgt, owner, tag).

// Integrity tag rules.

isIntegTag(integTag) :- directHasIntegTag(_, integTag).

// The inverse integrity tag is the same text as the integrity tag except with
// a '!' in front of it.
integTagToInv(integTag, cat("!", integTag)) :- isIntegTag(integTag).

// Introduce an inverse integrity tag at any access point that has no incoming
// edges and is not directly given the corresponding integrity tag.
mayHaveInvIntegTag(ap, invIntegTag) :-
  isAccessPath(ap), integTagToInv(integTag, invIntegTag), !edge(_, ap),
  !directHasIntegTag(ap, integTag).

// Introduce inverse integrity tag wherever a corresponding integrity tag is
// removed.
mayHaveInvIntegTag(ap, invIntegTag) :-
  isAccessPath(ap), integTagToInv(integTag, invIntegTag),
  removeIntegTag(ap, integTag).

// Propagate inverse integrity tags along edges to any destination that did not
// have an integrity tag directly added.
mayHaveInvIntegTag(dst, invIntegTag) :-
  isAccessPath(src), isAccessPath(dst), edge(src, dst), mayHaveInvIntegTag(src, invIntegTag),
  integTagToInv(integTag, invIntegTag), !directHasIntegTag(dst, integTag).

// An access path must have an integrity tag if it may not have the
// corresponding inverse integrity tag.
mustHaveIntegTag(ap, integTag) :-
  isAccessPath(ap), integTagToInv(integTag, invIntegTag), !mayHaveInvIntegTag(ap, invIntegTag).

//-----------------------------------------------------------------------------
// Universe Populating Rules
//-----------------------------------------------------------------------------
// These rules add objects to the set of things that exist (i.e. relations like
// `isObject(..)`) whenever they are mentioned in other rules.

// Any Principal used in a claim is in the universe of isPrincipal.
isPrincipal(principal) :- says_hasTag(principal, _, _, _).

isAccessPath(path) :- says_hasTag(_, path, _, _).

// Symbols used in hasTag or mayHaveTag are tags
isTag(tag) :- says_hasTag(_, _, _, tag).

// A pair of (base, member) is in this set if the base access path is a non-trivial subpath of the
// member access path.
.decl isMemberOf(base: AccessPath, member: AccessPath)

// The member is a member of the base if it starts with the base plus '.'.
isMemberOf(base, member) :-
  isAccessPath(base),
  isAccessPath(member),
  strlen(base) + 1 < strlen(member),
  cat(base, ".") = substr(member, 0, strlen(base) + 1).

// An access path may have a tag if some subpath to a member field has that tag. This allows
// checking for some inner node in the access path whether any leaf node of that node might have
// a particular tag.
mayHaveTag(base, owner, tag) :-
  isAccessPath(base),
  isAccessPath(member),
  mayHaveTag(member,owner, tag),
  isMemberOf(base, member).

#endif // SRC_ANALYSIS_SOUFFLE_TAINT_DL_
