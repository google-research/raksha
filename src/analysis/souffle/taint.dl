//-----------------------------------------------------------------------------
// Copyright 2021 Google LLC
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     https://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
//-----------------------------------------------------------------------------

#ifndef SRC_ANALYSIS_SOUFFLE_TAINT_DL_
#define SRC_ANALYSIS_SOUFFLE_TAINT_DL_

#include "src/analysis/souffle/authorization_logic.dl"
#include "src/analysis/souffle/check_predicate.dl"
#include "src/analysis/souffle/dataflow_graph.dl"
#include "src/analysis/souffle/tags.dl"
#include "src/analysis/souffle/operations.dl"

//-----------------------------------------------------------------------------
// Relation Declarations
//-----------------------------------------------------------------------------

// Indicates that an access path definitely has a tag. Differs from mayHaveTag
// below in that mayHaveTag indicates only that it is possible that an
// accessPath has a tag.
.decl hasTag(accessPath: AccessPath, owner: Principal, tag: Tag)

// Declares that `accessPath` may have been tainted with `tag`.
// If false, `accessPath` *definitely* does not have `tag` taint at runtime.
// If true, `accessPath` may have the `tag` taint at runtime.
// NOTE: In practice there may be value in explicitly having a layer of
// indirection in which accessPaths have IFCLabels and just IFCLabels have Tags.
// In this formalization, accessPaths also act as IFCLabels (in the sense that
// they have Tags).
.decl mayHaveTag(accessPath: AccessPath, owner: Principal, tag: Tag)

// The tag for some accessPath is actually removed (by taking into consideration
// the privileges of a party trying to remove it, and as a result of that party
// deciding to remove it). Because here IFC labels are sets of tags (ordered
// by the subset or equals relation), removing a tag from the set corresponds to
// a downgrade by making set of tags on the access path "lower" in the order.
.decl removeTag(path: AccessPath, owner: Principal, tag: Tag)


// Manifests can produce base facts of this form, where usually the principal
// is a particle (and this fact is entered by a code reviewer with no knowledge
// of the taint analysis).
.decl claimNotEdge(principal: Principal, src: AccessPath, dst: AccessPath)

// Indicates that an integrity tag is directly applied to an AccessPath. This
// rule should only be populated with base facts with no derivation to prevent
// us from getting tangled up in stratification issues. Propagation will happen
// via the lacksIntegrityTag rule.
.decl hasAppliedIntegrityTag(accessPath: AccessPath, owner: Principal, integTag: IntegrityTag)

// Integrity tag universe.
.decl isIntegrityTag(integTag: IntegrityTag)

// Removes an integrity tag from some path.
.decl removeIntegrityTag(accessPath: AccessPath, owner: Principal, integTag: IntegrityTag)

// Given how predecessors are ordered in orderedPredecessors, indicate whether
// a predecessor with a given orderNum and all predecessors with lower
// orderNums have a particular IntegrityTag.
.decl predecessorsUpToOrderNumHaveIntegrityTag(
  accessPath: AccessPath, owner: Principal, integTag: IntegrityTag, orderNum: number)

// Used to test whether a particular path has a particular integrity tag.
.decl mustHaveIntegrityTag(accessPath: AccessPath, owner: Principal, integTag: IntegrityTag)

//-----------------------------------------------------------------------------
// Rules
//-----------------------------------------------------------------------------

// Draw an edge from any operation's operand to its result.
resolvedEdge(owner, src, tgt) :- operationToOperands(owner, _, tgt, src).

// For all operations other than `=`, draw an edge from the special
// "ArbitraryComputation" `AccessPath` to the result. This prevents default
// propagation of `IntegrityTag`s through the operation.
isAccessPath("ArbitraryComputation").
resolvedEdge(owner, "ArbitraryComputation", tgt) :-
  operationToOperands(owner, operand, tgt, _), operand != "=".

ownsAccessPath(principal, path) :- says_ownsAccessPath(principal, principal, path).
ownsAccessPath(principal, tgt) :-
  resolvedEdge(principal, src, tgt), ownsAccessPath(principal, src).

// This rule says that a tag may be removed only if the owner of the tag says so.
removeTag(path, owner, tag) :-
   says_removeTag(owner, path, owner, tag).

hasTag(path, owner, tag) :- says_hasTag(owner, path, owner, tag).

mayHaveTag(tgt, owner, tag) :- hasTag(tgt, owner, tag).

mayHaveTag(tgt, owner, tag) :-
    resolvedEdge(owner, src, tgt), mayHaveTag(src, owner, tag), !removeTag(tgt, owner, tag).

// Integrity tag rules.
isIntegrityTag(integTag) :- hasAppliedIntegrityTag(_, _, integTag).
isPrincipal(principal) :- hasAppliedIntegrityTag(_, principal, _).

predecessorsUpToOrderNumHaveIntegrityTag(ap, owner, integTag, 0) :-
  orderedPredecessors(ap, owner, pred, 0),
  mustHaveIntegrityTag(pred, owner, integTag).

predecessorsUpToOrderNumHaveIntegrityTag(ap, owner, integTag, ordNum + 1) :-
  predecessorsUpToOrderNumHaveIntegrityTag(ap, owner, integTag, ordNum),
  orderedPredecessors(ap, owner, pred, ordNum + 1),
  mustHaveIntegrityTag(pred, owner, integTag).

// Any access path that has an IntegrityTag directly applied must have the
// integrity tag.
mustHaveIntegrityTag(ap, owner, integTag) :- hasAppliedIntegrityTag(ap, owner, integTag).

// If all predecessors up to the one with an order number one less than the
// number of predecessors (ie, all predecessors) have the integrity tag and the
// integrity tag is not removed, then this AccessPath has the IntegrityTag.
mustHaveIntegrityTag(ap, owner, integTag) :-
  isAccessPath(ap), isIntegrityTag(integTag), isPrincipal(owner),
  numPredecessors(ap, owner, numPreds),
  numPreds > 0,
  predecessorsUpToOrderNumHaveIntegrityTag(ap, owner, integTag, numPreds - 1),
  !removeIntegrityTag(ap, owner, integTag).

//-----------------------------------------------------------------------------
// Universe Populating Rules
//-----------------------------------------------------------------------------
// These rules add objects to the set of things that exist (i.e. relations like
// `isObject(..)`) whenever they are mentioned in other rules.

// Any Principal used in a claim is in the universe of isPrincipal.
isPrincipal(principal) :- says_hasTag(principal, _, _, _).

isAccessPath(path) :- says_hasTag(_, path, _, _).

// Symbols used in hasTag or mayHaveTag are tags
isTag(tag) :- says_hasTag(_, _, _, tag).

// A pair of (base, member) is in this set if the base access path is a non-trivial subpath of the
// member access path.
.decl isMemberOf(base: AccessPath, member: AccessPath)

// The member is a member of the base if it starts with the base plus '.'.
isMemberOf(base, member) :-
  isAccessPath(base),
  isAccessPath(member),
  strlen(base) + 1 < strlen(member),
  cat(base, ".") = substr(member, 0, strlen(base) + 1).

// An access path may have a tag if some subpath to a member field has that tag. This allows
// checking for some inner node in the access path whether any leaf node of that node might have
// a particular tag.
mayHaveTag(base, owner, tag) :-
  isAccessPath(base),
  isAccessPath(member),
  mayHaveTag(member,owner, tag),
  isMemberOf(base, member).

predicateEval($CP_TagPresence(ap, owner, tag), 1) :-
  checkSubExprs($CP_TagPresence(ap, owner, tag)), mayHaveTag(ap, owner, tag).

predicateEval($CP_TagPresence(ap, owner, tag), 0) :-
  checkSubExprs($CP_TagPresence(ap, owner, tag)), !mayHaveTag(ap, owner, tag).

#endif // SRC_ANALYSIS_SOUFFLE_TAINT_DL_
