//-----------------------------------------------------------------------------
// Copyright 2021 Google LLC
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     https://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
//-----------------------------------------------------------------------------
#ifndef SRC_ANALYSIS_SOUFFLE_DATAFLOW_GRAPH_DL_
#define SRC_ANALYSIS_SOUFFLE_DATAFLOW_GRAPH_DL_

// We will use the access path abtraction to refer to data during analysis.
// Consider the following data schema (not a valid syntax):
//  schema A {
//    a: Integer
//    b: schema B {
//      bf: String
//    }
//  }
//
// Suppose that data with schema A is stored in a database that is accessible
// through a handle `h`. `h.a`, `h.b`, and `h.b.bf` are all examples of access
// paths. For the time being, we will use a `symbol` to represent access paths.
// However, in the long term, it would make sense to define an ADT for this.
//
.type AccessPath <: symbol
.decl isAccessPath(path: AccessPath)

// A data flow edge.
.decl edge(src: AccessPath, tgt: AccessPath)

// Use the previous relations to map an edge to a generated access path
// representing its midpoint.
.decl edgeToMidpointAccessPath(src: AccessPath, tgt: AccessPath, midpoint: AccessPath)

// An "resolvedEdge" is an internal concept. When the user requests an edge with an
// edge fact, we will expand that into resolvedEdges in one of two ways:
//
// 1. The edge is not the subject of a claimNotEdge, in which case an
// resolvedEdge directly corresponding to the edge is created.
//
// 2. The edge is the subject of a claimNotEdge, in which case three facts
// are created: a generated midway point AccessPath, an resolvedEdge going from the
// src of the original edge to the midway point, and the midway point AccessPath
// to the tgt. That allows us to effectively eliminate edges that are the
// subject of a claimNotEdge from the dataflow graph by eliminating all tags
// from the midway point. Paths are defined in terms of resolvedEdges.
.decl resolvedEdge(for: Principal, src: AccessPath, tgt: AccessPath)

// A direct or transitive data flow path.
.decl path(src: AccessPath, tgt: AccessPath)

// A common way to do "forall" in Datalog is to use !exists. Unfortunately,
// this imposes a stratification requirement. To allow IntegrityTags to
// propagate when all predecessors have a particular IntegrityTag, instead give
// each predecessor an arbitrary predecessor number between 0 and N, where N is
// the number of predecessors a particular AccessPath has. Track the number of
// predecessors an AccessPath has in numPredecssors. This allows us to
// calculate whether all predecessors up to a particular order number have an
// integrity tag, with the base case of 0 being just "does that particular
// AccessPath have the IntegrityTag" and the inductive case being "does the nth
// predecessor have the IntegrityTag and all predecessors up to N-1 have the
// IntegrityTag". We can then ask if all predecessors up to numPredecessors
// - 1 have the integrity tag to accomplish forall.
.decl orderedPredecessors(ap: AccessPath, for: Principal, src: AccessPath, orderNum: number)
.decl numPredecessors(ap: AccessPath, for: Principal, num: number)

//-----------------------------------------------------------------------------
// Rules
//-----------------------------------------------------------------------------

resolvedEdge(principal, src, tgt) :-
   isPrincipal(principal),
   edge(src, tgt),
   !claimNotEdge(principal, src, tgt).

// Transitive paths
path(from, to) :- resolvedEdge(_, from, to).
path(from, to) :- resolvedEdge(_, from, intermediate), path(intermediate, to).

// Symbols used in resolvedEdges are access paths
isAccessPath(x) :- resolvedEdge(_, x, _).
isAccessPath(y) :- resolvedEdge(_, _, y).

// The 0th predecessor shall be the predecessor with the smallest src ordinal
// number.
orderedPredecessors(ap, for, src, 0) :-
  resolvedEdge(for, src, ap),
  ord(src) = min ord(candidate) : { resolvedEdge(for, candidate, ap) }.

// The Nth precessor shall be the precessor with the smallest src ordinal
// number larger than the N-1th ordinal number.
orderedPredecessors(ap, for, src, prevOrd + 1) :-
  orderedPredecessors(ap, for, prevSrc, prevOrd),
  resolvedEdge(for, src, ap),
  ord(src) = min ord(candidate) : { resolvedEdge(for, candidate, ap), ord(candidate) > ord(prevSrc) }.

numPredecessors(ap, for, maxOrd + 1) :-
  orderedPredecessors(ap, for, _, maxOrd),
  maxOrd = max ordNum : { orderedPredecessors(ap, for, _, ordNum) }.

#endif // SRC_ANALYSIS_SOUFFLE_DATAFLOW_GRAPH_DL_
