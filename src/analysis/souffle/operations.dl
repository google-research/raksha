//-----------------------------------------------------------------------------
// Copyright 2021 Google LLC
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     https://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
//-----------------------------------------------------------------------------
#ifndef SRC_ANALYSIS_SOUFFLE_OPERATIONS_DL_
#define SRC_ANALYSIS_SOUFFLE_OPERATIONS_DL_

#include "src/analysis/souffle/attributes.dl"
#include "src/analysis/souffle/dataflow_graph.dl"

// The symbol representing the operator. For operations commonly represented by
// a single character operator (such as +, =, /, *, etc), the operator will be
// the tsring containing that character.
.type Operator <: symbol

// A list of operands, used for gathering all operators in a single relation
// regardless of arity. Useful for rules that apply to any operation.
.type OperandList = [ operand: AccessPath, next: OperandList ]

// An Operation.
.type Operation = [owner: Principal, operator: Operator, result: AccessPath,
                   operandList: OperandList, attributes: AttributeList]

// Operation universe.
.decl isOperation(operation: Operation)

// OperandList universe.
.decl isOperandList(operandList: OperandList)

// Operator universe.
.decl isOperator(operator: Operator)

// Attribute list universe.
.decl isAttributeList(attributeList: AttributeList)

// Fill the appropriate universes with the pieces of the operation.
isPrincipal(owner), isOperator(operator), isAccessPath(result),
isOperandList(operandList), isAttributeList(attributeList) :-
isOperation([owner, operator, result, operandList, attributeList]).

// Declare mappings from an operation to its fields.
.decl operationHasResult(operation: Operation, result: AccessPath)
.decl operationHasOwner(operation: Operation, owner: Principal)
.decl operationHasOperator(operation: Operation, operator: Operator)
.decl operationHasOperandList(operation: Operation, operandList: OperandList)
.decl operationHasAttributeList(operation: Operation, attributeList: AttributeList)

// Populate field association relations.
operationHasResult(operation, result), operationHasOwner(operation, owner),
operationHasOperator(operation, operator), operationHasOperandList(operation, operands),
operationHasAttributeList(operation, attributes) :-
  isPrincipal(owner), isOperator(operator), isAccessPath(result), isOperandList(operands),
  isAttributeList(attributes), isOperation(operation),
  operation = [owner, operator, result, operands, attributes].

// BINARY_OPERATION is a convenience macro for the very-common-case of binary operations.
#define BINARY_OPERATION(owner, operator, result, attrs, input1, input2) \
isOperation([owner, operator, result, [input1, [input2, nil]], attrs])

// Explode the operand lists so that we can extract the heads of each sublist
// to get a mapping from operations to their operands.
.decl operationHasOperandListSuffix(operation: Operation, partialList: OperandList)

operationHasOperandListSuffix(operation, operandList) :-
  operationHasOperandList(operation, operandList).
operationHasOperandListSuffix(operation, tail) :-
  operationHasOperandListSuffix(operation, [_, tail]).

// `listLength` is the length of the `operandList`.
.decl operandListLength(operandList: OperandList, listLength: number)

operandListLength(nil, 0).
operandListLength([head, tail], tailLen + 1) :-
  operationHasOperandListSuffix(_, [head, tail]), operandListLength(tail, tailLen).

// `operandListLength` is the length of the `operandList` on the given
// `operation`.
.decl operationOperandListLength(operation: Operation, operandListLength: number)

operationOperandListLength(operation, len) :-
  isOperation(operation), operationHasOperandList(operation, operandList),
  operandListLength(operandList, len).

// True when an `operation` has the given `operand` at the indicated `index` in
// its `operandList`.
.decl operationHasOperandAtIndex(operation: Operation, operand: AccessPath, index: number)

isAccessPath(operand) :- operationHasOperandAtIndex(_, operand, _).

// To find the index of an operand in an operation, find the sublist that it is
// the head of in the operation, find the length of that list, and subtract it
// from the overall length of the operand list in the operation.
operationHasOperandAtIndex(operation, operand, opListLen - suffixListLen) :-
  operationHasOperandListSuffix(operation, [operand, tail]),
  operandListLength([operand, tail], suffixListLen),
  operationOperandListLength(operation, opListLen).

// A helper relation that produces all partial attribute lists of an
// `Operation`.
.decl operationHasAttributeListSuffix(operation: Operation, attributes: AttributeList)
operationHasAttributeListSuffix(operation, attrs) :-
  isOperation(operation), operationHasAttributeList(operation, attrs).
operationHasAttributeListSuffix(op, tail) :- operationHasAttributeListSuffix(op, [_, tail]).

// A mapping from an `Operation` to each individual `Attribute` on that
// `Operation`.
.decl operationHasAttribute(op: Operation, attr: Attribute)
operationHasAttribute(op, attr) :- operationHasAttributeListSuffix(op, [attr, _]).

// Indicates that this operator has special internal edges defined and should
// not be subject to the default rules.
.decl hasSpecialInternalEdges(operator: Operator)

#endif // SRC_ANALYSIS_SOUFFLE_OPERATIONS_DL_
