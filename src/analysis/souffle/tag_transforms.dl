//-----------------------------------------------------------------------------
// Copyright 2022 Google LLC
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     https://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
//-----------------------------------------------------------------------------
#ifndef SRC_ANALYSIS_SOUFFLE_TAG_TRANSFORMS_DL_
#define SRC_ANALYSIS_SOUFFLE_TAG_TRANSFORMS_DL_

#include "src/analysis/souffle/operations.dl"
#include "src/analysis/souffle/tags.dl"

// Note: this will need to change when we integrate auth logic into the SQL
// verifier.
#define DEFAULT_SQL_OWNER as("sql", Principal)
#define TAG_TRANSFORM_OPERATOR as("sql.tag_transform", Operator)
#define MERGE_OPERATOR as("sql.merge", Operator)
#define TAG_TRANSFORM_RULE_NAME_ATTR "rule_name"
#define CONTROL_INPUT_START_INDEX_NAME_ATTR "control_input_start_index"

// Tag transforms are very special in their propagation of tags: their
// precondition inputs do not propagate confidentiality tags, as they aren't
// part of the "real" dataflow. They are only used to indicate where a policy
// rule should check for integrity tags preconditions. In addition, any tag
// transform should propagate any integrity tags from its transformed input to
// the output.
hasSpecialEdgeRules(TAG_TRANSFORM_OPERATOR).

// A precondition produced in the SQL verifier. The `preconditionName`
// corresponds to the user-provided name in the SQL pattern in the original
// policy rule.
.type SqlPolicyRulePrecondition = [ preconditionName : symbol, requiredTag: IntegrityTag ]
.type SqlPolicyRulePreconditionList =
  [ precondition: SqlPolicyRulePrecondition, next: SqlPolicyRulePreconditionList ]

.type SqlPolicyRuleResult =
  AddIntegrityTag { tag: IntegrityTag }
  | RemoveConfidentialityTag { tag: Tag }
  // Note: In the SQL verifier, rules can only add confidentiality tags on
  // named columns of global tables.
  | AddConfidentialityTag { tag: Tag }

// A policy rule, as expressed by the SQL verifier.
.type SqlPolicyRule =
  [ policyRuleName: symbol,
    result: SqlPolicyRuleResult,
    preconditions: SqlPolicyRulePreconditionList ]

.decl isSqlPolicyRule(rule: SqlPolicyRule)

.decl sqlPolicyRuleNameHasPreconditionList(
  policyRuleName: symbol, policyRuleListSuffix: SqlPolicyRulePreconditionList)

sqlPolicyRuleNameHasPreconditionList(name, preconditionList) :-
  isSqlPolicyRule([name, _, preconditionList]).

.decl sqlPolicyRuleNameHasPreconditionListSuffix(
  policyRuleName: symbol, policyRuleListSuffix: SqlPolicyRulePreconditionList)

sqlPolicyRuleNameHasPreconditionListSuffix(name, preconditionList) :-
  sqlPolicyRuleNameHasPreconditionList(name, preconditionList).

sqlPolicyRuleNameHasPreconditionListSuffix(name, preconditionListTail) :-
  sqlPolicyRuleNameHasPreconditionListSuffix(name, [_, preconditionListTail]).

.decl policyRulePreconditionListLength(
  policyRuleList: SqlPolicyRulePreconditionList, length: number)

policyRulePreconditionListLength(nil, 0).
policyRulePreconditionListLength([head, tail], tailLen + 1) :-
  sqlPolicyRuleNameHasPreconditionListSuffix(_, [head, tail]),
  policyRulePreconditionListLength(tail, tailLen).

.decl sqlPolicyRuleNameHasPreconditionAtIndex(
  policyRuleName: symbol, policyRulePrecondition: SqlPolicyRulePrecondition, index: number)

sqlPolicyRuleNameHasPreconditionAtIndex(name, precondition, suffixLen - 1) :-
  sqlPolicyRuleNameHasPreconditionListSuffix(name, [precondition, tail]),
  policyRulePreconditionListLength([precondition, tail], suffixLen).

.decl sqlPolicyRuleNameHasResult(policyRuleName: symbol, result: SqlPolicyRuleResult)

sqlPolicyRuleNameHasResult(name, result) :- isSqlPolicyRule([name, result, _]).

isTag(tag) :- sqlPolicyRuleNameHasResult(_, $AddConfidentialityTag(tag)).
isTag(tag) :- sqlPolicyRuleNameHasResult(_, $RemoveConfidentialityTag(tag)).
isIntegrityTag(tag) :- sqlPolicyRuleNameHasResult(_, $AddIntegrityTag(tag)).

.decl isTagTransformOperation(tagTransform: Operation)

isTagTransformOperation(
  [DEFAULT_SQL_OWNER, TAG_TRANSFORM_OPERATOR, result, operandList, attributes]) :-
  isOperation([DEFAULT_SQL_OWNER, TAG_TRANSFORM_OPERATOR, result, operandList, attributes]).

// A TagTransform operator propagates confidentiality tags and integrity tags
// only from its direct input (its 0th input) to its output. It faithfully
// propagates all integrity tags. This can be expressed by drawing a single
// edge from the input to the output.
resolvedEdge(DEFAULT_SQL_OWNER, xformedInput, xformedOutput) :-
  isTagTransformOperation(operation), operationHasOperandAtIndex(operation, xformedInput, 0),
  operationHasResult(operation, xformedOutput).

.type TagPrecondition = [ path: AccessPath, itag: IntegrityTag ]

.decl tagTransformHasRuleName(tagTransform: Operation, ruleName: symbol)

tagTransformHasRuleName(op, ruleName) :-
  isTagTransformOperation(op),
  operationHasAttribute(op, [TAG_TRANSFORM_RULE_NAME_ATTR, $StringAttributePayload(ruleName)]).

// Associates the tagTransform operation with a tagPrecondition that must be
// met for the transform to occur and the index of the corresponding policy
// rule precondition in the relevant SqlPolicyRule record. We need the index to
// allow us to check that all preconditions have been satisfied without using
// negation (which could cause problems with stratification requirements).
.decl tagTransformHasIndexedTagPrecondition(
  tagTransform: Operation, index: number, tagPrecondition: TagPrecondition)

tagTransformHasIndexedTagPrecondition(op, index, [preconditionOperand, requiredTag]) :-
  isTagTransformOperation(op),
  tagTransformHasRuleName(op, ruleName),
  operationHasAttribute(op, [preconditionName, $NumberAttributePayload(operandIndex)]),
  preconditionName != TAG_TRANSFORM_RULE_NAME_ATTR,
  operationHasOperandAtIndex(op, preconditionOperand, operandIndex),
  sqlPolicyRuleNameHasPreconditionAtIndex(ruleName, [preconditionName, requiredTag], index).

.decl allTagTransformPreconditionsLessThanIndexMet(tagTransform: Operation, index: number)
allTagTransformPreconditionsLessThanIndexMet(op, 0) :- isTagTransformOperation(op).
allTagTransformPreconditionsLessThanIndexMet(op, idx + 1) :-
  isTagTransformOperation(op),
  tagTransformHasIndexedTagPrecondition(op, idx, [preconditionPath, requiredITag]),
  mustHaveIntegrityTag(preconditionPath, DEFAULT_SQL_OWNER, requiredITag),
  allTagTransformPreconditionsLessThanIndexMet(op, idx).

.decl allTagTransformPreconditionsMet(tagTransform: Operation)

allTagTransformPreconditionsMet(tagTransform) :-
  isTagTransformOperation(tagTransform),
  tagTransformHasRuleName(tagTransform, ruleName),
  sqlPolicyRuleNameHasPreconditionList(ruleName, preconditionList),
  policyRulePreconditionListLength(preconditionList, preconditionListLength),
  allTagTransformPreconditionsLessThanIndexMet(tagTransform, preconditionListLength).

removeTag(result, DEFAULT_SQL_OWNER, tag) :-
  isTagTransformOperation(op),
  operationHasResult(op, result),
  tagTransformHasRuleName(op, ruleName),
  allTagTransformPreconditionsMet(op),
  sqlPolicyRuleNameHasResult(ruleName, $RemoveConfidentialityTag(tag)).

hasTag(result, DEFAULT_SQL_OWNER, tag) :-
  isTagTransformOperation(op),
  operationHasResult(op, result),
  tagTransformHasRuleName(op, ruleName),
  allTagTransformPreconditionsMet(op),
  sqlPolicyRuleNameHasResult(ruleName, $AddConfidentialityTag(tag)).

mustHaveIntegrityTag(result, DEFAULT_SQL_OWNER, iTag) :-
  isTagTransformOperation(op),
  operationHasResult(op, result),
  tagTransformHasRuleName(op, ruleName),
  allTagTransformPreconditionsMet(op),
  sqlPolicyRuleNameHasResult(ruleName, $AddIntegrityTag(iTag)).

// In the SQL verifier, we have some merge operations that we would like to
// propagate integrity tags in a non-default fashion (ie, rather than dropping
// the integrity tags, we'd like them to intersect or union across all direct
// inputs). To do that, we introduce a tag transform with special behavior:
// rather than linking to a `SqlPolicyRule` fact, they shall instead link to
// built-in rules here that modify how the integrity tags propagate across the
// wrapped merge operation.

#define RAKSHA_INTERNAL_INTERSECT_ITAG_RULE_NAME "##raksha_internal#intersect_itag_rule"
#define RAKSHA_INTERNAL_UNION_ITAG_RULE_NAME "##raksha_internal#union_itag_rule"

.decl isSpecialITagPropagationRule(ruleName: symbol)
isSpecialITagPropagationRule(RAKSHA_INTERNAL_INTERSECT_ITAG_RULE_NAME).
isSpecialITagPropagationRule(RAKSHA_INTERNAL_UNION_ITAG_RULE_NAME).

.decl isMergeOp(mergeOp: Operation)
isMergeOp(mergeOp) :- isOperation(mergeOp), operationHasOperator(mergeOp, MERGE_OPERATOR).

.decl isModifiedPropagationMergeOp(mergeOp: Operation, ruleName: symbol)
isModifiedPropagationMergeOp(mergeOp, ruleName) :-
  isTagTransformOperation(tagTransformOp),
  tagTransformHasRuleName(tagTransformOp, ruleName),
  isSpecialITagPropagationRule(ruleName),
  operationHasOperandAtIndex(tagTransformOp, transformedInput, 0),
  operationHasResult(mergeOp, transformedInput),
  isMergeOp(mergeOp).

.decl mergeOperationHasControlInputStartIndex(mergeOp: Operation, controlStartIndex: number)
mergeOperationHasControlInputStartIndex(mergeOp, controlStartIdx) :-
  isMergeOp(mergeOp),
  operationHasAttribute(mergeOp,
    [CONTROL_INPUT_START_INDEX_NAME_ATTR, $NumberAttributePayload(controlStartIdx)]).

.decl isMergeOperationDirectInput(mergeOp: Operation, ap: AccessPath)
isMergeOperationDirectInput(mergeOp, inputPath) :-
  isMergeOp(mergeOp), isAccessPath(inputPath), operationHasOperandAtIndex(mergeOp, inputPath, idx),
  mergeOperationHasControlInputStartIndex(mergeOp, controlStartIdx),
  idx < controlStartIdx.

// For the union rule, any itag on a direct input should propagate to the
// output.
mustHaveIntegrityTag(result, DEFAULT_SQL_OWNER, iTag) :-
  isModifiedPropagationMergeOp(mergeOp, RAKSHA_INTERNAL_UNION_ITAG_RULE_NAME),
  operationHasResult(mergeOp, result),
  isMergeOperationDirectInput(mergeOp, operand),
  mustHaveIntegrityTag(operand, DEFAULT_SQL_OWNER, iTag).

// For intersection, an integrity tag must be present on all direct inputs to be
// placed upon the output. Use induction to check that it is present on all
// direct inputs up to the length of the list.
.decl mergeOperationHasIntegrityTagOnDirectOperandsUpToIndex(
  mergeOp: Operation, integrityTag: IntegrityTag, index: number)

// Base case, the 0th operand.
mergeOperationHasIntegrityTagOnDirectOperandsUpToIndex(mergeOp, integrityTag, 0) :-
  isMergeOp(mergeOp), isIntegrityTag(integrityTag),
  operationHasOperandAtIndex(mergeOp, operand, 0),
  isMergeOperationDirectInput(mergeOp, operand),
  operationHasOwner(mergeOp, owner),
  mustHaveIntegrityTag(operand, owner, integrityTag).

// Inductive case.
mergeOperationHasIntegrityTagOnDirectOperandsUpToIndex(mergeOp, integrityTag, idx + 1) :-
  isMergeOp(mergeOp), isIntegrityTag(integrityTag),
  mergeOperationHasIntegrityTagOnDirectOperandsUpToIndex(mergeOp, integrityTag, idx),
  operationHasOperandAtIndex(mergeOp, operand, idx + 1),
  isMergeOperationDirectInput(mergeOp, operand),
  operationHasOwner(mergeOp, owner), mustHaveIntegrityTag(operand, owner, integrityTag).

// Propagate the integrity tag if the operation has INTERSECTION propagation
// behavior and the integrity tag is present on all operands at indices up to
// just under the control start index.
mustHaveIntegrityTag(result, owner, integTag) :-
  isModifiedPropagationMergeOp(mergeOp, RAKSHA_INTERNAL_INTERSECT_ITAG_RULE_NAME),
  isPrincipal(owner), isIntegrityTag(integTag), operationHasResult(mergeOp, result),
  mergeOperationHasIntegrityTagOnDirectOperandsUpToIndex(mergeOp, integTag, idx),
  mergeOperationHasControlInputStartIndex(mergeOp, idx + 1).

#undef DEFAULT_SQL_OWNER
#undef TAG_TRANSFORM_OPERATOR
#undef TAG_TRANSFORM_RULE_NAME_ATTR

#endif // SRC_ANALYSIS_SOUFFLE_TAG_TRANSFORMS_DL_
