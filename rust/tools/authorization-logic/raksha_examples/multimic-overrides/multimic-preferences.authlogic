.decl attribute hasPrivacyPreference(subject : symbol, pref : symbol)
.decl attribute setsPrivacyResolution(subject : symbol, resolution_strategy : symbol)
.decl attribute may(action : symbol, tag : symbol)
.decl attribute ownsDevice(device_name : symbol)

.decl overridePreferences(subject : symbol)
.decl will(action : symbol, tag : symbol)
.decl hasTag(x : symbol, y : symbol)
.decl ownsTag(owner : principal, y : symbol)
.decl downgrades(path : symbol, tag : symbol)
.decl allowTranscription(p : principal)

.decl isAction(action : symbol)
.decl isPath(path : symbol)
.decl isTag(tag : symbol)
.decl isPrincipal(p : principal)
.decl isSpeechTranscript(x : symbol)
.decl isUserTranscript(x : symbol)
.decl isPrivacyPreference(pref : symbol)
.decl isPrivacyResolution(resolution : symbol)
.decl isUserSpeech(x : symbol)

// When "UserX" enters the room, a runtime on the user's
// mobile device sends a packet to the microphone which contains the following
// facts serialized (and with relevant signatures):
// "MobileDeviceRuntime" says {
//     hasTag("MicIn.audio", "UserX_userSpeech").
// }
// - Also passes along the facts by "UserX" (which are signed with UserX's key).
//

// The end users all delegate to a policy principal that decides what can be
// done with their data. Delegation to this policy principal happens when they 
// buy the device or decide to create an account within this privacy framework.
// It can be thought of as a mechanized EULA. The users then express their 
// preferences about how their data can be handled to the policy principal,
// but these preferences are not always met.

// The users give two preferences:
//  - hasPrivacyPreference(<type_of_data>, <preference_of_handling>) which 
//  describes how this user would like the policy framework to handle its own 
//  data. The three preferences are:
//     "permissive" -- the user permits anything to be done with their data
//     "only_store_transcript" -- only transcripts can be stored, not audio
//     "only_notify" -- the only thing that can be done with audio is notify 
//     there is a microphone
//  - setsPrivacyResolution(<type_of_data>, <resolution_suggestion>) gives the 
//  device owner a way to decide how disputes among user policies are handled
//  (cases where not everyone's preference can be met). The types of
//  resolution are:
//     "meet_requests" -- only allow actions everyone has consented to
//     "stay_on" -- the microphone must stay on even if this goes agaist the 
//     preferences of some users in the room

"UserA" says {
  ownsTag("UserA", "UserA_userSpeech").
  ownsTag("UserA", "UserA_speechTranscript").

  // UserA expresses that its preference about how audio is handled is 
  // permissive. Any action can be done with it and it can be transformed in 
  // any way.
  "UserA" hasPrivacyPreference("audio", "permissive").
  // UserA expresses that transcription and storage of transcripts should stay 
  // on even if this conflicts with the requests of others.
  "UserA" setsPrivacyResolution("audio", "stay_on").

  // User A gives the policy principal the right to decide that any action can 
  // be done with UserA's data and to transform this data in any way.
  "AmbientPrivacyPolicy" canSay PrincipalX may(ActionX, TagX) :-
    isAction(ActionX), isPrincipal(PrincipalX), isTag(TagX).
  "AmbientPrivacyPolicy" canSay downgrades(PathX, TagX) :-
    isPath(PathX), isTag(TagX).
  "AmbientPrivacyPolicy" canSay hasTag(PathX, TagX) :-
    isPath(PathX), isTag(TagX).

  // This is stuff that needs to be generated behind the scenes
  // (Clearly A's policy should have no knowledge of this)
  isAction("deliver_privacy_notifications").
  isAction("local_computation").
  isAction("store").
  isPrincipal("AmbientPrivacyPolicy").
  isPrincipal("Notifier").
  isPrincipal("MicCompute").
  isPrincipal("SpeechToTextStorageSink").
  isPath("audioIn").
  isPath("asrStorageConn").
  isPath("speechToTextIn").
  isTag("UserA_userSpeech").
  isTag("UserA_speechTranscript").
}

// UserB is a guest user. UserB is somewhat privacy sensitive. They want their
// voice recorded only as a transcript and will only recognize authorities from
// companies that they trust.
"UserB" says {
  ownsTag("UserB", "UserB_userSpeech").
  ownsTag("UserB", "UserB_speechTranscript").

  // UserB's preference for handling audio is to only store transcripts of what 
  // was said, but not raw audio.
  "UserB" hasPrivacyPreference("audio", "only_store_transcript").
  // UserB says that disputes should be handled by meeting the requests of all 
  // people in the room.
  "UserB" setsPrivacyResolution("audio", "meet_requests").

  // User B gives the policy principal the right to decide that any action can 
  // be done with UserB's data and to transform this data in any way.
  "AmbientPrivacyPolicy" canSay PrincipalX may(ActionX, TagX) :-
    isAction(ActionX), isPrincipal(PrincipalX), isTag(TagX).
  "AmbientPrivacyPolicy" canSay downgrades(PathX, TagX) :-
    isPath(PathX), isTag(TagX).
  "AmbientPrivacyPolicy" canSay hasTag(PathX, TagX) :-
    isPath(PathX), isTag(TagX).

  // This is stuff that needs to be generated behind the scenes
  // (Clearly B's policy should have no knowledge of this)
  isAction("deliver_privacy_notifications").
  isAction("local_computation").
  isAction("store").
  isPrincipal("AmbientPrivacyPolicy").
  isPrincipal("Notifier").
  isPrincipal("MicCompute").
  isPrincipal("SpeechToTextStorageSink").
  isPath("audioIn").
  isPath("asrStorageConn").
  isPath("speechToTextIn").
  isTag("UserB_userSpeech").
  isTag("UserB_speechTranscript").
}

// UserC is a very privacy sensitive user. They do not trust anyone to store
// their voice information, even as a transcript. They recognize policy
// authorities only to get those policy authorities to not record their voice
// or to get notifications that their voice may be being recorded so they can
// leave.
"UserC" says {
  ownsTag("UserC", "UserC_userSpeech").
  ownsTag("UserC", "UserC_speechTranscript").

  // UserC's preference for how to handle audio is to only use it to notify 
  // them that the 
  "UserC" hasPrivacyPreference("audio", "only_notify").
  // UserC says that disputes should be handled by meeting the requests of all 
  // the people in the room.
  "UserC" setsPrivacyResolution("audio", "meet_requests").

  // User C gives the policy principal the right to decide that any action can 
  // be done with UserC's data and to transform this data in any way.
  "AmbientPrivacyPolicy" canSay PrincipalX may(ActionX, TagX) :-
    isAction(ActionX), isPrincipal(PrincipalX), isTag(TagX).
  "AmbientPrivacyPolicy" canSay downgrades(PathX, TagX) :-
    isPath(PathX), isTag(TagX).
  "AmbientPrivacyPolicy" canSay hasTag(PathX, TagX) :-
    isPath(PathX), isTag(TagX).
  
  // This is stuff that needs to be generated behind the scenes
  // (Clearly C's policy should have no knowledge of this)
  isAction("deliver_privacy_notifications").
  isAction("local_computation").
  isAction("store").
  isPrincipal("AmbientPrivacyPolicy").
  isPrincipal("Notifier").
  isPrincipal("MicCompute").
  isPrincipal("SpeechToTextStorageSink").
  isPath("audioIn").
  isPath("asrStorageConn").
  isPath("speechToTextIn").
  isTag("UserC_userSpeech").
  isTag("UserC_speechTranscript").
}

// The facts here are produced by some part of the microphone's operating 
// system. This module is trusted by the "AmbientPrivacyPolicy"
// to correctly determine which user owns the device.
"DeviceRoleAuthority" says {
  "UserA" ownsDevice("this_microphone").
}

// This is the (presumably open-source) privacy policy "EULA" that users 
// delegate to.
"AmbientPrivacyPolicy" says {

  // This policy allows end-users to express their own preferences and state 
  // their own preference for how dispute resolution should be done. 
  UserX canSay UserX hasPrivacyPreference("audio", PreferenceX) :-
    isPrivacyPreference(PreferenceX), isPrincipal(UserX).
  UserX canSay UserX setsPrivacyResolution("audio", ResolutionX) :-
    isPrivacyResolution(ResolutionX), isPrincipal(UserX).

  "DeviceRoleAuthority" canSay UserX ownsDevice("this_microphone") :-
    isPrincipal(UserX).

  // The privacy preferences of some members of the room might not be met if 
  // the owner of the device says that it needs to stay on.
  overridePreferences("audio") :- UserX ownsDevice("this_microphone"),
    UserX setsPrivacyResolution("audio", "stay_on").

  // Regardless of preferences, notification that there is a microphone is 
  // always allowed.
  "Notifier" may("deliver_privacy_notifications", UserX_userSpeech) :-
    isUserSpeech(UserX_userSpeech).

  // The pipleine involving transcription and storage of the transcript is 
  // allowed to run for a particular user if:
  // 1) the user's preference is either permissive or that just transcripts can
  // be stored.
  // 2) the device owner's preference implies we should override the 
  // preferences of others
  // The rule "allow_transcription" captures these alternatives:
  allowTranscription(UserX) :-
    UserX hasPrivacyPreference("audio", "only_store_transcript").
  allowTranscription(UserX) :- UserX hasPrivacyPreference("audio", "permissive").
  allowTranscription(UserX) :- overridePreferences("audio"), isPrincipal(UserX).

  "MicCompute" may("local_computation", UserX_userSpeech) :-
    isUserSpeech(UserX_userSpeech), ownsTag(UserX, UserX_userSpeech),
    allowTranscription(UserX).
  
  "MicCompute" canSay downgrades("asrStorageConn", UserX_userSpeech) :-
    isUserSpeech(UserX_userSpeech), ownsTag(UserX, UserX_userSpeech),
    allowTranscription(UserX).

  "MicCompute" canSay hasTag("asrStorageConn", UserX_speechTranscript) :-
    isUserTranscript(UserX_speechTranscript), ownsTag(UserX, UserX_speechTranscript),
    allowTranscription(UserX).
  
  "SpeechToTextStorageSink" may("store", UserX_speechTranscript) :-
    isUserTranscript(UserX_speechTranscript), ownsTag(UserX, UserX_speechTranscript),
    allowTranscription(UserX).

  // Should be generated behind the scenes:
  isPrincipal("UserA").
  isPrincipal("UserB").
  isPrincipal("UserC").
  isPrivacyPreference("permissive").
  isPrivacyPreference("only_store_transcript").
  isPrivacyPreference("only_notify").
  isPrivacyResolution("stay_on").
  isPrivacyResolution("meet_requests").
  isUserSpeech("UserA_userSpeech").
  isUserSpeech("UserB_userSpeech").
  isUserSpeech("UserC_userSpeech").
  ownsTag("UserA", "UserA_userSpeech").
  ownsTag("UserB", "UserB_userSpeech").
  ownsTag("UserC", "UserC_userSpeech").
  isUserTranscript("UserA_speechTranscript").
  isUserTranscript("UserB_speechTranscript").
  isUserTranscript("UserC_speechTranscript").
  ownsTag("UserA", "UserA_speechTranscript").
  ownsTag("UserB", "UserB_speechTranscript").
  ownsTag("UserC", "UserC_speechTranscript").
}

//-----------------------------------------------------------------------------
// "Will" Expressions (that go near particles)
//-----------------------------------------------------------------------------
// These statements are also kept near the particles, but in order to actually 
// run this they are repeated here. These are used to generate the checks 
// against the permissions granted with "may"
"Notifier" says {
    will("deliver_privacy_notifications", "audioIn").
}

"MicCompute" says {
    will("local_computation", "audioIn"). 
    // Assumes we can figure out tags with namespaces like this:
    downgrades("asrStorageConn", UserX_userSpeech) :- 
      // ideally this predicate would be added behind the scenes:
      isUserSpeech(UserX_userSpeech).
    hasTag("asrStorageConn", UserX_userTranscript) :- 
      // should be added behind the scenes    
      isSpeechTranscript(UserX_userTranscript).

    // should be added behind the scenes:
    isUserSpeech("UserA_userSpeech").
    isUserSpeech("UserB_userSpeech").
    isUserSpeech("UserC_userSpeech").
    isSpeechTranscript("UserA_speechTranscript").
    isSpeechTranscript("UserB_speechTranscript").
    isSpeechTranscript("UserA_speechTranscript").
}
    
"SpeechToTextStorageSink" says {
   will("store", "speechToTextIn").
}

//-----------------------------------------------------------------------------
// Checks / Analysis
//-----------------------------------------------------------------------------

// The comments and queries below this line describe what the analysis / checks 
// should do. These are not part of the policy. In this scenario, A, B, and C 
// are all in the room, but because A has resolution "stay_on", all of these 
// checks are true (transcription and storage stay on), even though C does not 
// want this. If you change the line that sets "A"'s resolution policy to 
// "meet_requests", the checks for "C" other than notificaiton are false 
// (transcription and storage stay off).

// Notifier taint:
//  audioIn: UserA_userSpeech, UserB_userSpeech, UserC_userSpeech

// expect: true
may_notifierA = query "UserA" says
  "Notifier" may("deliver_privacy_notifications", "UserA_userSpeech")?

// expect: true
may_notifierB = query "UserB" says
  "Notifier" may("deliver_privacy_notifications", "UserB_userSpeech")?

// expect: true
may_notifierC = query "UserC" says
  "Notifier" may("deliver_privacy_notifications", "UserC_userSpeech")?

// MicCompute taint:
// audioIn: UserA_userSpeech, UserB_userSpeech, UserC_userSpeech

// expect: true
may_micComputeA = query "UserA" says
    "MicCompute" may("local_computation", "UserA_userSpeech")?

// expect: true
may_micComputeB = query "UserB" says
    "MicCompute" may("local_computation", "UserB_userSpeech")?

// expect: true (because preferences can be overridden)
may_micComputeC = query "UserC" says
    "MicCompute" may("local_computation", "UserC_userSpeech")?

// These are not checks generated by the "wills" but they test 
// downgrades that need to work for speech to text to have the right tags
// expect: true
down_micComputeA = query "UserA" says
    downgrades("asrStorageConn", "UserA_userSpeech")?

// expect: true
down_micComputeB = query "UserB" says
    downgrades("asrStorageConn", "UserB_userSpeech")?

// expect: true
down_micComputeC = query "UserC" says
    downgrades("asrStorageConn", "UserC_userSpeech")?

// SpeechToTextStorageSink taint:
// speechToTextIn: UserA_userTranscript, UserB_userTranscript, 
//      UserC_userTranscript

// expect: true
may_speechInA = query "UserA" says
    "SpeechToTextStorageSink" may("store", "UserA_speechTranscript")?

// expect: true
may_speechInB = query "UserB" says
    "SpeechToTextStorageSink" may("store", "UserB_speechTranscript")?

// expect: true
may_speechInC = query "UserC" says
    "SpeechToTextStorageSink" may("store", "UserC_speechTranscript")?
